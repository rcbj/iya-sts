# ---------------------------------------------------------------------------
# THE IMAGE REPOSITORY.
#
# Two images per commit: `<sha>` (the service, built with the AWS SDK and the
# RDS CA bundle) and `schema-<sha>` (the init container). Private, scanned on
# push, and trimmed so a CI job that builds on every run does not accumulate
# storage for ever.
# ---------------------------------------------------------------------------
resource "aws_ecr_repository" "main" {
  name                 = var.name
  image_tag_mutability = "MUTABLE"

  image_scanning_configuration {
    scan_on_push = true
  }

  encryption_configuration {
    encryption_type = "KMS"
    kms_key         = aws_kms_key.main.arn
  }
}

resource "aws_ecr_lifecycle_policy" "main" {
  repository = aws_ecr_repository.main.name
  policy = jsonencode({
    rules = [{
      rulePriority = 1
      description  = "Keep the 30 most recent images"
      selection = {
        tagStatus   = "any"
        countType   = "imageCountMoreThan"
        countNumber = 30
      }
      action = { type = "expire" }
    }]
  })
}
